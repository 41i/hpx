#!/usr/bin/perl
#
# Copyright (c) 2008 Steve Brandt
#
# Distributed under the Boost Software License, Version 1.0. (See accompanying 
# file LICENSE_1_0.txt or copy at http://www.boost.org/LICENSE_1_0.txt)
#
# Program for executing hpx under PBS
# Starts a agas server, as well as an hpx_runtime on each node
# except the mother superior, where the user application is
# started instead.
#
# Needs getopts and a full listing of possible options. For
# now it's just:
#   hpx_exec user_app
#
use strict;
use FileHandle;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper;

my $agas_port = 7912;
my $parcel_port = 7913;
my $host_suffix = "";

my $node_file = $ENV{PBS_NODEFILE};
my $result = GetOptions(
    "agas_port|a=i" => \$agas_port,
    "parcel_port|p=i" => \$parcel_port,
    "host_suffix|h=s" => \$host_suffix,
    "help|?" => sub { pod2usage(-verbose => 2, -exitval=> 2); },
    "node_file|n=s" => \$node_file) or pod2usage(2);

my $prog = $ARGV[0];

print Dumper($result);
print Dumper(\@ARGV);

#pod2usage(-verbose => 1,  -exitval=>2) unless($#ARGV == 0 and -e $ARGV[0]);

unless(-e $node_file) {
    pod2usage(-verbose => 1, -exitval => 2);
}

my $hpx_runtime = find("hpx_runtime");
my $user_prog = find($prog);
my $ssh = find("ssh");

my $fd = new FileHandle;
my @nodes = ();
my %nodes = ();
open($fd,$node_file) or die "Could not read node file";
while(my $node = <$fd>) {
    chomp($node);
    unless(defined($nodes{$node})) {
        push @nodes, $node;
    }
    $nodes{$node}++;
}
close($fd);

my @pids = ();

# Start the Agas server
my $anode = $nodes[0].$host_suffix;
my $pid = fork();
if($pid == 0) {
    run($hpx_runtime,"-r","-a","$anode:$agas_port");
    exit(0);
};
push @pids, $pid;

# Start the hpx procs -- one per node, using -t $tc to pass thread count
for(my $i=$#nodes;$i>=0;$i--) {
    my $node = $nodes[$i].$host_suffix;
    my $id = $i+1;

    # Find the thread count
    my $tc = $nodes{$node};

    my $pid = fork();
    my $cmd = $user_prog;
    
    if($i > 0) {
        $cmd = $hpx_runtime;
    }
    if($pid == 0) {
        run($ssh,"-o","NumberOfPasswordPrompts=0",$node,$cmd,"-a","$anode:$agas_port","-x","$node:$parcel_port","-t","$tc"); 
    };
    push @pids, $pid;
}
print "all runtime engines started\n";
for my $pid (@pids) {
    waitpid($pid,0);
}
print "all runtimes stopped.\n";

sub run {
    print join(" ",@_),"\n";
    exec(@_);
    die "Exec failed.";
}

sub find {
    my $file = shift;
    for my $path (split(/:/,$ENV{PATH})) {
        my $cmd = "$path/$file";
        return $cmd if(-x $cmd and !-d $cmd);
    }
    die "could not locate $file on path";
}
__END__

=head1 NAME

hpx_exec - launch hpx jobs under PBS

=head1 SYNOPSIS

hpx_exec [options] [file...]

=head1 OPTIONS

Options:
    -agas_port|a=i
    -parcel_port|p=i
    -host_suffix|h=s
    -help|?
    -node_file|n=s

=head1 DESCRIPTION

stuff

=cut
